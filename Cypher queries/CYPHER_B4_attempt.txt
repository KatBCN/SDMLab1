Authors with number of papers:

MATCH(a:Author)-[published:WROTE]->(p:Paper)
RETURN a.name, count(published) as published
ORDER BY published DESC

Papers with number of citations:

MATCH(p:Paper)<-[cites:CITES]-(p2:Paper)
RETURN p, count(cites) as citations
ORDER BY citations DESC

Authors with Number of Papers and List of Numbers of Citations:

MATCH(a:Author)-[published:WROTE]->(p:Paper)
CALL{
    with p
    MATCH(p)<-[cites:CITES]-(p2:Paper)
    RETURN count(p2) as numCitations
    ORDER BY numCitations DESC
}
RETURN a.name as Author, size(collect(p)) as numPapers, collect(numCitations)
ORDER BY numPapers DESC

Authors with Number of Papers and SORTED List of Numbers of Citations:

MATCH(a:Author)-[wrote:WROTE]->(p:Paper)
OPTIONAL MATCH (p)<-[cites:CITES]-(p2:Paper)
WITH count(cites) as numCites,a,p
ORDER BY numCites DESC
return a.name as Author, size(collect(p)) as numPapers, (collect(numCites)) as CitationList
ORDER BY numPapers DESC

Authors with H-index:

MATCH(a:Author)-[wrote:WROTE]->(p:Paper)
OPTIONAL MATCH (p)<-[cites:CITES]-(p2:Paper)
WITH count(cites) as numCites,a,p
ORDER BY numCites DESC
WITH a.name as Author, (collect(numCites)) as CitationList
WITH [i IN RANGE(0, SIZE(CitationList)-1) WHERE CitationList[i] >= i] as H_index, Author
UNWIND H_index as h
RETURN Author, max(h) as H_index
ORDER BY H_index DESC